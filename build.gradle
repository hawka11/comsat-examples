import org.gradle.api.artifacts.*

apply plugin: 'base' // To add "clean" task to the root project.

subprojects {
    apply from: rootProject.file('common.gradle')

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()

            force "javax.servlet:javax.servlet-api:$servletVer"
            force "org.eclipse.jetty:jetty-http:$jettyVer"
            force "org.eclipse.jetty:jetty-util:$jettyVer"
            force "org.eclipse.jetty:jetty-io:$jettyVer"
            force "io.dropwizard.metrics:metrics-core:$metricsVer"
            force "org.jdbi:jdbi:$jdbiVer"
            force "org.slf4j:slf4j-api:$slf4jVer"
            force "org.apache.httpcomponents:httpcore:$httpCoreVer"
            force "org.apache.httpcomponents:httpclient:$httpClientVer"
         }
    }
}

task mergedJavadoc(type: Javadoc, description: 'Creates Javadoc from all the projects.') {
    title = 'All modules'
    destinationDir = new File(project.buildDir, 'merged-javadoc')

    // Note: The closures below are executed lazily.
    source {
       subprojects*.sourceSets*.main*.allSource
    }
    classpath.from {
        subprojects*.configurations*.compile*.copyRecursive({ !(it instanceof ProjectDependency); })*.resolve()
    }
}
